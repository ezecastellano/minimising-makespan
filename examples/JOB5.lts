const CantTask = 5
range Tasks = 1..CantTask
const Limit = 6
range Memory = 0..Limit

set Controllable = {s.[a:Tasks],g}
set NotControllable = {e.[a:Tasks]}
set Alphabet = {Controllable}

TASK(ID=1) = (s.[ID] -> e.[ID] -> g -> TASK).

MEM = RAM[0],
RAM[u:Memory] = (s.[a:Tasks] -> RAM[a+u] | e.[a:Tasks] -> RAM[u-a]).

||Plant = (forall[t:Tasks] TASK(t) || MEM).

fluent PROCESSING[i:Tasks] = <s.[i],e.[i]>
assert KEEP_WORKING = forall[i:1..CantTask] PROCESSING[i]
assert ALLFINISH = forall[i:1..CantTask] FINISH[i]
fluent GOAL= <g, Alphabet\{g}>

fluent START[i:Tasks] = <s.[i],g>
fluent FINISH[i:Tasks] = <e.[i],g>

fluent PROCESSING_3 = <s.[3],e.[3]>

ltl_property S2AftF1 = [] (START[2] -> FINISH[1])
ltl_property S1AftS4orS3 = [] (START[1] -> START[3] || START[4])
ltl_property S5AftF3orS2 = [] (START[5] -> FINISH[3] || START[2])

ltl_property AllFinished = [] (X g -> ALLFINISH)

controller ||C = (Plant)~{G1}.
checkCompatibility ||Compatible = (Plant)~{G1}.

|| K = (C || C).

controllerSpec G1 = {
	safety = {S2AftF1, S1AftS4orS3, S5AftF3orS2, AllFinished}
	liveness = {GOAL}
	activityDefinitions = {KEEP_WORKING}
	controllable = {Controllable}
}
